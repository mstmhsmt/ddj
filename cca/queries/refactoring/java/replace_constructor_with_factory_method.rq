
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?ctor ?meth_ ?mname_ ?cname
(?class AS ?originalClass)
(?class_ AS ?modifiedClass)
(?ctor AS ?removedCtor)
(?meth_ AS ?factoryMethod)
(?mname_ AS ?methodName)
(?cname AS ?className)
(?cname AS ?GROUP)
WHERE {

  ?class a java:ClassDeclaration ;
         java:fullyQualifiedName ?cname ;
         chg:mappedTo ?class_ .

  ?class_ a java:ClassDeclaration ;
          java:fullyQualifiedName ?cname_ .

  ?ctor a java:ConstructorDeclaration ;
        java:inClass ?class ;
        chg:removal ?e0_ .

  ?meth_ a java:MethodDeclaration ;
         java:name ?mname_ ;
         src:child2 ?ty_ ;
         java:inClass ?class_ .

  ?static a java:Static ;
          java:inMethod ?meth_ .
         
  ?ty_ a java:ReferenceType ;
       src:parent ?meth_ ;
       java:name ?cname_ .

}
