
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

# ?decl -- deleted variable declarator
# ?v -- declared variable
# ?qmeth_ -- added method

SELECT DISTINCT ?decl ?qmeth_ ?v_name ?v ?invoke_ ?mname
(?v_name AS ?variableName)
(?decl AS ?originalDeclarator)
(?qmeth_ AS ?addedMethod)
(?qmeth_name_ AS ?addedMethodName)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {

  ?decl a java:VariableDeclarator ;
        java:inMethodOrConstructor ?meth ;
        chg:deletedFrom ?e0_ .

  ?meth java:name ?mname ;
        chg:mappedTo ?meth_ .

  ?v a java:Name ;
     java:declaredBy ?decl ;
     java:name ?v_name ;
     ?modified ?invoke_ .

  FILTER (?modified IN (chg:changedTo, chg:modified))

  ?invoke_ a java:StandardMethodInvocation ;
           java:name ?qmeth_name_ .

  ?qmeth_ a java:MethodDeclaration ;
         java:name ?qmeth_name_ ;
         chg:insertedInto ?class .

  ?decl java:initializer ?rhs .

  ?e_ java:inMethod ?qmeth_ .

  ?rhs chg:mappedTo ?e_


}
