
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

# ?decl_ -- inserted variable declarator
# ?v_    -- declared variable
# ?f     -- mapped to the entity into which ?v_ is inserted
# ?a     -- moved expression

SELECT DISTINCT ?a ?rhs_ ?vname_ ?decl_ ?f ?f_ ?v_
(?a AS ?originalExpr) (?rhs_ AS ?movedExpr)
(?f AS ?originalContext) (?f_ AS ?modifiedContext)
(?v_ AS ?introducedVariable)
(?vname_ AS ?introducedVariableName)
(?decl_ AS ?addedVDecl)
(?cname AS ?className)
(?mname AS ?methodName)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(CONCAT(?className,".",?methodName) AS ?GROUP)
WHERE {

  {
    SELECT DISTINCT ?vdtor_ ?e ?meth ?meth_ ?class ?cname ?v_ ?f_ ?vname_
    WHERE {

      ?vdtor_ a java:VariableDeclarator ;
              java:inClassBodyDeclaration ?meth_ ;
              chg:addition ?e .

      ?meth java:inTypeDeclaration ?class ;
            chg:mappedTo ?meth_ .

      ?class a java:TypeDeclaration ;
             java:name ?cname .

      ?v_ java:declaredBy ?vdtor_ ;
          src:parent ?f_ ;
          java:name ?vname_ .

    } GROUP BY ?vdtor_ ?e ?meth ?meth_ ?class ?cname ?v_ ?f_ ?vname_
  }

  {
    ?v_ chg:insertedOrGrafted ?f .
  }
  UNION
  {
    ?v chg:relabeled ?v_ ;
       src:parent ?f .
  }

  ?a src:parent ?f .

  ?vdtor_ java:initializer ?rhs_ .

  {
    ?a src:parent*/chg:mappingChange ?rhs_ .
  }
  UNION
  {
     ?a src:value ?h .
     ?x_ src:parent* ?rhs_ ;
         src:value ?h .
  }
  UNION
  {
     ?a src:treeDigest ?h .
     ?x_ src:parent* ?rhs_ ;
         src:treeDigest ?h .
  }

  ?f java:inClassBodyDeclaration ?meth .

  OPTIONAL {
    ?meth java:name ?mname .
  }

  FILTER NOT EXISTS {
    ?vdtor_ src:parent/src:parent [ a java:ForInit ];
            java:inStatement ?for_ .
    ?for_ a java:BasicForStatement ;
          ^chg:mappedTo ?for .
    ?for a java:ForStatement .
  }

  FILTER NOT EXISTS {
    ?a java:inStatement ?for .
    {
      ?for a java:BasicForStatement ;
           src:child0|src:child1|src:child2 ?for_x .
    }
    UNION
    {
      ?for a java:EnhancedForStatement ;
           src:child0|src:child1 ?for_x .
    }
    ?a src:parent* ?for_x .
  }

}

