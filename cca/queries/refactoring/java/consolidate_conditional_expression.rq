
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?if0 ?if_ ?mname
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?if0 AS ?originalIf)
(?if_ AS ?modifiedIf)
(?mname AS ?GROUP)
WHERE {

  ?class a java:ClassDeclaration ;
         ver:version ?ver ;
         chg:mappedTo ?class_ .

  ?class_ a java:ClassDeclaration ;
          ver:version ?ver_ .

  ?if0 a java:IfStatement ;
       java:inMethod ?meth ;
       java:genRemoved ?e0_ .

  ?meth a java:MethodDeclaration ;
        java:name ?mname ;
        java:inClass ?class ;
        chg:mappedTo ?meth_ .

  ?meth_ a java:MethodDeclaration ;
         java:inClass ?class_ .

  ?cond0 java:conditionOf ?if0 .

  ?then0 java:thenPartOf ?if0 ;
         a ?cat_then .

  ?if1 a java:IfStatement ;
       java:inMethod ?meth ;
       java:genRemoved ?e1_ .

  ?cond1 java:conditionOf ?if1 .

  ?then1 java:thenPartOf ?if1 ;
         a ?cat_then .

  FILTER (?if0 != ?if1)


  ?if_ java:genAdded ?e2 ;
       java:inMethod ?meth_ .

  ?if_ a java:IfStatement .
  {
    ?cond0 chg:mappedTo ?cond0_ .
    ?cond1 chg:mappedTo ?cond1_ .
    ?cond0_ java:inStatement ?if_ .
    ?cond1_ java:inStatement ?if_ .
  }
  UNION
  {
    ?cond_ java:conditionOf ?if_ ;
           a java:MethodInvocation .
  }

}
