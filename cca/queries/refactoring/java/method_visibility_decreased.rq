
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>

SELECT DISTINCT ?meth ?meth_ ?mods ?mods_
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?originalMethodName)
(?mname_ AS ?modifiedMethodName)
(?mname AS ?GROUP)
WHERE {

  ?meth a java:MethodOrConstructor ;
        java:inTypeDeclaration/ver:version ?ver ;
        java:fullyQualifiedName ?mname ;
        chg:mappedTo ?meth_ .

  ?meth_ a java:MethodOrConstructor ;
         java:inTypeDeclaration/ver:version ?ver_ ;
         java:fullyQualifiedName ?mname_ .

  ?ver ver:next ?ver_ .

  ?mods a java:Modifiers ;
        src:parent ?meth ;
        chg:mappedTo ?mods_ .

  ?mods_ a java:Modifiers ;
         src:parent ?meth_ .

  {
    ?mod a java:Public ;
         src:parent ?mods ;
         chg:removal ?ctx_ .

    FILTER NOT EXISTS {
      ?mod_ a java:Public ;
            src:parent ?mods_ .
    }
  }
  UNION
  {
    ?mod a java:Modifier ;
         a ?mcat OPTION (INFERENCE NONE) ;
         src:parent ?mods ;
         chg:relabeled ?mod_ .

    ?mod_ a java:Modifier ;
          a ?mcat_ OPTION (INFERENCE NONE) .

    FILTER ((?mcat = java:Public && (?mcat_ IN (java:Protected, java:Private))) ||
            (?mcat = java:Protected && ?mcat_ = java:Private))

  }
  UNION
  {
    ?mod_ a java:Private ;
         src:parent ?mods_ ;
         chg:removal ?ctx .

    FILTER NOT EXISTS {
      ?mod a java:Private ;
           src:parent ?mods .
    }
  }
  UNION
  {
    ?mod_ a java:Protected ;
          src:parent ?mods_ ;
          chg:removal ?ctx .

    FILTER NOT EXISTS {
      ?mod a java:Protected ;
           src:parent ?mods .
    }
  }

  FILTER EXISTS {
    ?ivk java:mayInvokeMethod ?meth ;
         chg:mappedTo ?ivk_ .
    ?ivk_ java:mayInvokeMethod ?meth_ .
  }

}
