
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?field1 ?e0_ ?field0 ?field0_ ?cname0 ?cname1
(?cname0 AS ?otherClassName)
(?cname1 AS ?className)
(?class1 AS ?originalClass)
(?class1_ AS ?modifiedClass)
(?field AS ?removedField)
(?e0_ AS ?modifiedContext)
(?cname0 AS ?GROUP)
WHERE {

  {
    SELECT DISTINCT ?class1 ?class1_ ?field1 ?cname1 ?cname1_ ?e0_
    WHERE {

      ?field1 a java:FieldDeclaration ;
              java:inClass ?class1 ;
              chg:deletedOrPruned ?e0_ .

      ?class1 a java:ClassDeclaration ;
              java:fullyQualifiedName ?cname1 ;
              chg:mappedTo ?class1_ .

      ?class1_ a java:ClassDeclaration ;
               java:fullyQualifiedName ?cname1_ .

    } GROUP BY ?class1 ?class1_ ?field1 ?cname1 ?cname1_ ?e0_
  }

  ?e0_ ver:version ?ver_ .
  ?class1 ver:version ?ver .
  ?class1_ ver:version ?ver_ .

  ?ver ver:next ?ver_ .

  {
    SELECT DISTINCT ?cname1 ?field0 ?field0_ ?class0 ?class0_ ?cname0
    WHERE {

      ?ty0 a java:ReferenceType ;
           src:parent ?field0 ;
           java:name ?cname1 .

      ?field0 java:inClass ?class0 ;
              a java:FieldDeclaration ;
              chg:mappedTo ?field0_ .

      ?class0 a java:ClassDeclaration ;
              java:fullyQualifiedName ?cname0 ;
              ver:version ?ver ;
              chg:mappedTo ?class0_ .

    } GROUP BY ?cname1 ?field0 ?field0_ ?class0 ?class0_ ?cname0
  }

  FILTER (?cname0 != ?cname1)

  FILTER EXISTS {
    ?class0 ver:version ?ver .  
    ?class0_ ver:version ?ver_ .
  }

  FILTER EXISTS {
    ?field0_ a java:FieldDeclaration ;
             java:inClass ?class0_ .
  }

  FILTER EXISTS {
    ?class0_ a java:ClassDeclaration ;
             java:fullyQualifiedName ?cname0_ .

    ?ty1 a java:ReferenceType ;
         src:parent ?field1 ;
         java:name ?cname0 .

    FILTER (?cname0_ != ?cname1_)
  }

}

