
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?param ?param_ ?v ?v_
(?v AS ?originalVariable)
(?v_ AS ?modifiedVariable)
(?pname AS ?originalParameterName)
(?pname_ AS ?modifiedParameterName)
(?param AS ?originalParameter)
(?param_ AS ?modifiedParameter)
(?cname AS ?className)
(?mname AS ?methodName)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?class AS ?originalClass)
(CONCAT(?className,".",?methodName) AS ?GROUP)
WHERE {

  ?param a java:Parameter ;
        java:inMethodOrConstructor ?meth ;
        chg:relabeled ?param_ .

  ?param_ a java:Parameter ;
          java:inMethodOrConstructor ?meth_ .

  ?v java:declaredBy ?param ;
     java:name ?pname ;
     chg:relabeled ?v_ .

  ?v_ java:declaredBy ?param_ ;
      java:name ?pname_ .

  ?meth java:inTypeDeclaration ?class ;
        chg:mappedTo ?meth_ .

  OPTIONAL {
    ?meth java:name ?mname .
  }

  ?class a java:TypeDeclaration ;
         java:name ?cname .

}

