
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>



SELECT DISTINCT ?const ?ident_ ?name_ ?mname
(?name_ AS ?constantName)
(?meth_or_ctor AS ?originalMethod)
(?meth_or_ctor_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {

  ?const a ?lit ;
         java:inMethodOrConstructor ?meth_or_ctor ;
         chg:changedTo ?ident_ .

  FILTER (?lit IN (java:CharacterLiteral, 
                   java:FloatingPointLiteral, 
                   java:IntegerLiteral, 
                   java:StringLiteral
                  )
         )

  ?meth_or_ctor a java:MethodOrConstructor ;
                java:name ?mname ;
                chg:mappedTo ?meth_or_ctor_ .

  ?meth_or_ctor_ a java:MethodOrConstructor .

  ?ident_ a java:FieldAccess ;
          java:name ?name_ ;
          java:inMethodOrConstructor ?meth_or_ctor_ .

}
