
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?class0 ?class1_ ?cname0 ?cname1_
(?cname1_ AS ?extractedClassName)
(?class0 AS ?originalClass)
(?class1_ AS ?extractedClass)
(?cname0 AS ?className)
(?cname0 AS ?GROUP)
WHERE {


  {
    SELECT DISTINCT ?class0 ?class0_ ?cname0 ?ty0 ?cat_ty0 ?e0_
    WHERE {

      ?ty0 a java:Type ;
           a ?cat_ty0 OPTION (INFERENCE NONE) ;
           src:parent ?field0 .

      ?field0 a java:FieldDeclaration ;
              java:inClass ?class0 ;
              chg:removal ?e0_ .

      ?class0 a java:ClassDeclaration ;
              java:fullyQualifiedName ?cname0 ;
              chg:mappedTo ?class0_ .

      ?class0_ a java:ClassDeclaration .

    } GROUP BY ?class0 ?class0_ ?cname0 ?ty0 ?cat_ty0 ?e0_
  }

  ?e0_ ver:version ?ver_ .

  ?class0 ver:version ?ver .
  ?class0_ ver:version ?ver_ .

  ?ver ver:next ?ver_ .

  {
    SELECT DISTINCT ?class0_ ?cname1_ ?class1_ ?e1 ?e2
    WHERE {
  
      ?ty1_ src:parent ?field1_ ;
            a java:ReferenceType ;
            java:name ?cname1_ .

      ?field1_ a java:FieldDeclaration ;
               java:inClass ?class0_ ;
               chg:addition ?e1 .

      ?class0_ a java:ClassDeclaration .

      ?class1_ a java:ClassDeclaration ;
               java:fullyQualifiedName ?cname1_ ;
               chg:insertedInto ?e2 .

    } GROUP BY ?class0_ ?cname1_ ?class1_ ?e1 ?e2
  }

  FILTER EXISTS {
    ?class0_ ver:version ?ver_ .
    ?class1_ ver:version ?ver_ .

    ?e1 ver:version ?ver .
    ?e2 ver:version ?ver .
  }

  FILTER EXISTS {    
    ?field2_ a java:FieldDeclaration ;
             java:inClass ?class1_ .

    ?ty2_ a java:Type ;
          src:parent ?field2_ .
    { 
      ?ty2_ a java:PrimitiveType ;
            a ?cat_ty0 .
    } 
    UNION 
    { 
      ?ty0 java:name ?name_ty0 .
      ?ty2_ java:name ?name_ty0 .
    }
  }


}

