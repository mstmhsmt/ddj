
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?assign ?break_or_continue_ ?v_name ?mname ?dtor
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?break_or_continue_ AS ?introducedControl)
(?v_name AS ?flagName)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {


  ?ty a java:BooleanType ;
      java:inVariableDeclaration ?decl .

  ?decl a java:LocalVariableDeclaration ;
        java:inMethodOrConstructor ?meth .

  ?meth a java:MethodOrConstructor ;
        java:name ?mname .

  ?meth chg:mappedTo ?meth_ .

  ?dtor java:inVariableDeclaration ?decl ;
        a java:VariableDeclarator ;
        chg:removal ?e0_ .

  ?v a java:Name ;
     java:declaredBy ?dtor ;
     java:name ?v_name .

  ?assign src:children (?v ?rhs) ;
          a java:AssignmentOp .

  ?loop a java:LoopStatement ;
        chg:mappedNeqTo ?loop_ .

  ?assign src:parent+ ?loop .

  ?assign chg:mappedNeqTo ?break_or_continue_ .

  ?break_or_continue_ a ?cat OPTION (INFERENCE NONE). 

  FILTER (?cat IN (java:BreakStatement, java:ContinueStatement, java:ReturnStatement))


}
;
