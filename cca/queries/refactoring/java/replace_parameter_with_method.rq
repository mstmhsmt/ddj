
DEFINE input:inference "ont.cpi"

PREFIX fb:    <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?ivk ?ivk_ ?arg ?mname
(?ivk AS ?originalInvocation)
(?ivk_ AS ?modifiedInvocation)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {


  {
    SELECT DISTINCT ?meth ?meth_ ?mname ?mname_ ?ivk ?ivk_ ?args ?arg ?e0_
    WHERE {

      ?meth a java:MethodOrConstructor ;
            java:name ?mname ;
            chg:mappedTo ?meth_ .

      ?meth_ a java:MethodOrConstructor ;
             java:name ?mname_ .

      ?ivk a java:MethodInvocation ;
           java:inMethodOrConstructor ?meth ;
           java:name ?imname ;
           chg:mappedTo ?ivk_ .

      ?args a java:Arguments ;
            java:inInvocation ?ivk .

      ?arg src:parent ?args ;
           chg:prunedFrom ?e0_ .

    } GROUP BY ?meth ?meth_ ?mname ?mname_ ?ivk ?ivk_ ?args ?arg ?e0_
  }

  {
    ?arg a java:Name ;
         java:declaredBy ?dtor .

    ?dtor a java:VariableDeclarator ;
          java:initializer ?ivk0 .
  }
  UNION
  {
    ?ivk0 src:parent ?arg OPTION(TRANSITIVE, T_DISTINCT, T_MIN(0))
  }

  ?ivk0 a java:MethodInvocation ;
        chg:mappedTo ?ivk0_ .

  ?ivk0_ java:inMethodOrConstructor ?meth0_ .

  FILTER (?meth0_ != ?meth_)

  FILTER EXISTS {
    ?meth0_ java:name ?imname .
  }


}
