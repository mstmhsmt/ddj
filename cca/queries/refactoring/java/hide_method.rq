
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?meth ?meth_ ?mname ?mname_
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {

  ?mods a java:Modifiers ;
        src:parent ?meth .

  ?meth a java:MethodDeclaration ;
        java:fullyQualifiedName ?mname ;
        java:inClass ?class ;
        chg:mappedTo ?meth_ .

  ?meth_ a java:MethodDeclaration ;
         java:inClass ?class_ ;
         java:fullyQualifiedName ?mname_ .

  ?class a java:ClassDeclaration ;
         chg:mappedTo ?class_ .

  FILTER NOT EXISTS {
    ?priv a java:Private ;
          src:parent ?mods .
  }

  FILTER EXISTS {
    ?mods_ a java:Modifiers ;
           src:parent ?meth_ .

    ?priv_ a java:Private ;
           src:parent ?mods_ .

    { ?priv_ chg:insertedOrGrafted ?e0 }
    UNION
    { 
      ?mod a java:Modifier ;
           chg:mappedNeqTo ?priv_ .
    }
  }

}
