
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?class0_ ?class1 ?cname0_ ?cname1
(?class1 AS ?inlinedClass)
(?class0_ AS ?modifiedClass)
(?cname1 AS ?inlinedClassName)
(?cname0_ AS ?className)
(?cname1 AS ?GROUP)
WHERE {

  {
    SELECT DISTINCT ?class0 ?class0_ ?cname0_ ?ty0_ ?cat_ty0_ ?e0
    WHERE {

      ?ty0_ a java:Type ;
            a ?cat_ty0_ OPTION (INFERENCE NONE) ;
            src:parent ?field0_ .

      ?field0_ java:inClass ?class0_ ;
               a java:FieldDeclaration ;
               chg:addition ?e0 .

      ?class0_ a java:ClassDeclaration ;
               java:fullyQualifiedName ?cname0_ .

      ?class0 a java:ClassDeclaration ;
              chg:mappedTo ?class0_ .

    } GROUP BY ?class0 ?class0_ ?cname0_ ?ty0_ ?cat_ty0_ ?e0
  }

  ?e0 ver:version ?ver .

  ?class0 ver:version ?ver .
  ?class0_ ver:version ?ver_ .

  ?ver ver:next ?ver_ .

  {
    SELECT DISTINCT ?class0 ?cname1 ?class1 ?e1_ ?e2_
    WHERE {

      ?ty1 src:parent ?field1 ;
           a java:ReferenceType ;
           java:name ?cname1 .

      ?field1 a java:FieldDeclaration ;
              java:inClass ?class0 ;
              chg:removal ?e1_ .

      ?class0 a java:ClassDeclaration .

      ?class1 a java:ClassDeclaration ;
              java:fullyQualifiedName ?cname1 ;
              chg:deletedFrom ?e2_ .

    } GROUP BY ?class0 ?cname1 ?class1 ?e1_ ?e2_
  }

  FILTER EXISTS {
    ?class0 ver:version ?ver .
    ?class1 ver:version ?ver .

    ?e1_ ver:version ?ver_ .
    ?e2_ ver:version ?ver_ .
  }

  FILTER EXISTS {
    ?field2 a java:FieldDeclaration ;
            java:inClass ?class1 .

    ?ty2 a java:Type ;
         src:parent ?field2 .
    { 
      ?ty2 a java:PrimitiveType ;
           a ?cat_ty0_ .
    } 
    UNION 
    { 
      ?ty0_ java:name ?name_ty0_ .
      ?ty2 java:name ?name_ty0_ .
    }
  }


}
