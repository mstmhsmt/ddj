
DEFINE input:inference "ont.cpi"

PREFIX fb:   <http://codinuum.com/fb/>
PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?clientM ?clientM_ ?ExtC_ ?ExtCname_
(?clientM AS ?originalClientMethod)
(?clientM_ AS ?modifiedClientMethod)
(?ExtC_ AS ?introducedClass)
(?ExtCname_ AS ?introducedClassName)
(?ExtCname_ AS ?GROUP)
WHERE {

  {
    SELECT DISTINCT ?ExtC_ ?ExtCname_ ?extM_ ?ServerC_ ?e0
    ?extM_ ?clientM ?clientM_ ?ClientC
    ?ver ?ver_
    WHERE {

      {
        SELECT DISTINCT ?ExtC_ ?ExtCname_ ?extM_ ?ServerC_ ?e0
        WHERE {

          ?extM_ a java:MethodDeclaration ;
                 java:inClass ?ExtC_ .

          ?ExtC_ a java:ClassDeclaration ;
                 ver:version ?ver_ ;
                 java:fullyQualifiedName ?ExtCname_ ;
                 java:subClassOf ?ServerC_ ;
                 chg:insertedOrGrafted ?e0 .

        } GROUP BY ?ExtC_ ?ExtCname_ ?extM_ ?ServerC_ ?e0
      }

      ?e0 src:parent*/ver:version ?ver .

      ?ver ver:next ?ver_ .
      
      {
        SELECT DISTINCT ?extM_ ?clientM ?clientM_ ?ClientC
        WHERE {
          
          ?ent0_ a java:BlockStatement ;
                 java:inMethod ?extM_ .

          ?ent0 a java:BlockStatement ;
                java:inMethod ?clientM ;
                chg:mappedTo ?ent0_ .

          ?clientM a java:MethodDeclaration ;
                   java:inClass ?ClientC ;
                   chg:mappedTo ?clientM_ .

        } GROUP BY ?extM_ ?clientM ?clientM_ ?ClientC
      }

    } GROUP BY ?ExtC_ ?ExtCname_ ?extM_ ?ServerC_ ?e0
    ?extM_ ?clientM ?clientM_ ?ClientC
    ?ver ?ver_
  }

  ?ClientC a java:ClassDeclaration ;
             ver:version ?ver .

  OPTIONAL {
    SELECT DISTINCT ?ver ?ver_ ?ServerC ?ServerC_ ?ServerCname ?ServerCname_
    WHERE {

      ?ver ver:next ?ver_ .

      ?ServerC_ a java:ClassDeclaration ;
                ver:version ?ver_ ;
                java:fullyQualifiedName ?ServerCname_ .

      ?ServerC a java:ClassDeclaration ;
               ver:version ?ver ;
               java:fullyQualifiedName ?ServerCname ;
               chg:mappedTo ?ServerC_ .
      
    } GROUP BY ?ver ?ver_ ?ServerC ?ServerC_ ?ServerCname ?ServerCname_
  }

  FILTER EXISTS {
    ?ty a java:ReferenceType ;
        java:name ?ServerCname ;
        src:parent ?clientM .
  }


}
