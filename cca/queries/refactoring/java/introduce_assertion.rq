
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?meth ?assert_ ?mname
(?assert_ AS ?introducedAssertion)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {
  ?meth a java:MethodOrConstructor ;
        java:name ?mname ;
        chg:mappedTo ?meth_ .

  ?meth_ a java:MethodOrConstructor .

  {
    ?assert_ a java:AssertStatement ;
             chg:addition ?e0 ;
             java:inMethodOrConstructor ?meth_ .
  }
  UNION
  {
    ?assert_ a java:TypeMethodInvocation ;
             chg:addition ?e0 ;
             java:name ?aname_ ;
             java:inMethodOrConstructor ?meth_ .
    FILTER (regex(?aname_, 'Assert'))
  }

}
