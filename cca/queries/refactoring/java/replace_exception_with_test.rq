
DEFINE input:inference "ont.cpi"

PREFIX ent:  <http://codinuum.com/fact/entity/>
PREFIX rel:  <http://codinuum.com/fact/version/release/>
PREFIX ext:  <http://codinuum.com/fact/external/>
PREFIX src:  <http://codinuum.com/ontologies/2012/10/source-code-entity#>
PREFIX ver:  <http://codinuum.com/ontologies/2012/10/versioning#>
PREFIX chg:  <http://codinuum.com/ontologies/2012/10/primitive-change#>
PREFIX java: <http://codinuum.com/ontologies/2012/10/java-entity#>
PREFIX c:    <http://codinuum.com/ontologies/2012/10/c-entity#>


SELECT DISTINCT ?try ?if_ ?mname
(?if_ AS ?addedIf)
(?meth AS ?originalMethod)
(?meth_ AS ?modifiedMethod)
(?mname AS ?methodName)
(?mname AS ?GROUP)
WHERE {

  ?meth a java:MethodDeclaration ;
        java:name ?mname ;
        chg:mappedTo ?meth_ .

  ?try a java:TryStatement ;
       java:inMethod ?meth ;
       chg:deletedFrom ?e0_ .

  ?blk a java:Block ;
       src:parent ?try .

  ?ent src:parent ?blk OPTION(TRANSITIVE, T_DISTINCT, T_MIN(0)) .

  ?ent chg:mappedTo ?ent_ .

  ?ent_ src:parent ?if_ OPTION(TRANSITIVE, T_DISTINCT, T_MIN(1)) .

  ?if_ a java:IfStatement ;
       java:inMethod ?meth_ ;
       chg:addition ?e1 .

}
